import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.github.ben-manes.versions' version '0.24.0'
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

def today = Instant.now()
group 'net.syscon.elite'

def todayDate = LocalDate.now()
def formatter = DateTimeFormatter.ofPattern('yyyy-MM-dd')
version todayDate.format(formatter)

jar {
    archiveBaseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    compile.exclude module: 'commons-logging'
    compile.exclude module: 'log4j'
    compile.exclude module: 'c3p0'
    compile.exclude module: 'tomcat-jdbc'
}

// Exclude Serenity BDD integration and IntTest tests from 'test' task so they can be controlled independently
test {
    exclude '**/executablespecification/*', '**/*IntTest*'
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation files("${rootDir}/lib/ojdbc8-18.3.jar")

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.3.6.RELEASE'
    implementation 'org.springframework.security:spring-security-jwt:1.0.10.RELEASE'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'commons-codec:commons-codec:1.13'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9'
    implementation 'com.github.jsqlparser:jsqlparser:3.0'
    implementation 'net.sf.ehcache:ehcache:2.10.6'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.zaxxer:HikariCP:3.3.1'

    implementation 'io.swagger:swagger-jersey2-jaxrs:1.5.23'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.2'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.5.0-BETA.5'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.5.0-BETA.5'

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-text:1.8'

    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'

    compileOnly 'org.projectlombok:lombok:1.18.10'

    runtime 'org.hsqldb:hsqldb:2.4.1'
    runtime 'org.flywaydb:flyway-core:5.2.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'io.rest-assured:rest-assured:4.1.1'
    testImplementation 'io.rest-assured:json-schema-validator:4.1.1'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.1.1'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'

    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.8.0'

    testImplementation 'net.serenity-bdd:serenity-core:2.0.70'
    testImplementation 'net.serenity-bdd:serenity-junit:2.0.70'
    testImplementation 'net.serenity-bdd:serenity-spring:2.0.70'
    testImplementation 'net.serenity-bdd:serenity-cucumber:1.9.45'
    testImplementation 'org.slf4j:slf4j-api:1.7.25'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
}

task testIntegration(type: Test) {
    include '**/*IntTest*'
}

task testWithSchemaNomis(type: Test) {
    def nomisEnvVars = ['api.db.target'   : 'nomis',
                        'cucumber.options': "--tags 'not (@wip or @broken)'"]

    exclude '**/persistence/*'
    exclude '**/*IntTest*'
    environment nomisEnvVars
}

task testWithSchemaNomisOracle(type: Test) {
    def nomisEnvVars = ['api.db.target'   : 'nomis', 'api.db.dialect': 'oracle',
                        'cucumber.options': "--tags 'not (@wip or @broken)'"]

    exclude '**/persistence/*'
    exclude '**/*IntTest*'
    environment nomisEnvVars
}

test.dependsOn testIntegration, testWithSchemaNomis
